options
{
    output logfile = TRUE;
    output LaTeX = TRUE;
};

tryreduce
{
    K_d[], L_d[];
};

block CONSUMER
{
    definitions
    {
        u[] = gamma * log(C[]) + (1 - gamma) * log(1 - L_s[]) + D[];
    };
    controls
    {
        K_s[], C[], L_s[], I[];
    };
    objective
    {
        U[] = u[] + beta * E[][U[1]];
    };
    constraints
    {
        I[] + (1 + tau_c) * C[] = (1 - tau_k) * r[] * K_s[-1] + (1 - tau_l) * W[] * L_s[] + pi[] : lambda_c[];
        K_s[] = I[] + (1 - delta) * K_s[-1];
    };
    calibration
    {
        delta = 0.06;
        beta = 0.97;
        gamma = 0.40;
        tau_c = 0.116;
        tau_l = 0.348;
        tau_k = 0.225;
    };
};

block FIRM
{
    controls
    {
        K_d[], L_d[], Y[], pi[];
    };
    objective
    {
        PI[] = pi[];
    };
    constraints
    {
        Y[] = A[] * K_d[]^alpha * L_d[]^(1 - alpha);
        pi[] = Y[] - L_d[] * W[] - r[] * K_d[];
    };
    calibration
    {
        alpha = 0.350;
    };
};

block EQUILIBRIUM
{
    identities
    {
        K_d[] = K_s[-1];
        L_d[] = L_s[];
    };
};

block TECHNOLOGY
{
    identities
    {
        A[] = exp(rho * log(A[-1]) + epsilon_A[]);
    };
    shocks
    {
        epsilon_A[];
    };
    calibration
    {
        rho = 0.95;
    };
};

block TRANSFERS
{
    identities
    {
        D[] = rho_D * D[-1] + epsilon_D[];
    };
    shocks
    {
        epsilon_D[];
    };
    calibration
    {
        rho_D = 0.90;
    };
};
